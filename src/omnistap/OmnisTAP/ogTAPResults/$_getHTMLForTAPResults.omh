Begin text block
Text:<table>

For ilTAPResults.$line from 1 to ilTAPResults.$linecount step 1
	If isclear(ilTAPResults.Description)
		Calculate lcDescription as '<span class="missing_test_description">Missing test description</span>'
	Else
		Calculate lcDescription as $cinst.$_cleanHTML(ilTAPResults.Description)
	End If
	#//// Wrap the description in a link for jumping directly to the calling method
	Calculate lvURL as con('tap:goto_',OW3.$escapeuritext(con(ilTAPResults.Class,'|',ilTAPResults.Method,'|',ilTAPResults.Line)))
	Calculate lcDescription as con(' <a href="',lvURL,'" style="text-decoration:none;color:black;" target="_new" >',lcDescription,'</a>')
	
	Switch ilTAPResults.TAP
		Case "#"
			Text:<tr class="diagnostic" colspan="2">
			Text:<td class="[$cinst.$_getHTMLClassForDescription()]">
			Text:[lcDescription]
			Text:</td>
			Text:</tr>
		Case "ok"
			Text:<tr class="ok">
			Text:<td class="[$cinst.$_getHTMLClassForDescription()]">
			Text:[lcDescription]
			Text:</td>
			Text:[$cinst.$_getTDForOutcome(ilTAPResults.TAP,kTrue)]
			Text:</tr>
		Case "not ok"
			Text:<tr class="not_ok">
			Text:<td class="[$cinst.$_getHTMLClassForDescription()]">
			Text:[lcDescription]
			If not(isclear(ilTAPResults.Assertion))
				Text:<div class='test assertion failed'>[$cinst.$_cleanHTML(ilTAPResults.Assertion)]</div>
			End If
			Text:</td>
			Text:[$cinst.$_getTDForOutcome(ilTAPResults.TAP,kFalse)]
			Text:</tr>
	End Switch
End For

Text:</table>
End text block

Get text block lcHTMLForTAPResults

Quit method lcHTMLForTAPResults