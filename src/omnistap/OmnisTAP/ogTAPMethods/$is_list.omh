If not(isclear(pfrList1.$colcount)&(isclear(pfrList2.$colcount)))
	If isclear(pfrList1)&not(isclear(pfrList2))
		Do $cinst.$ok(kFalse,pcMessage,"Have a clear list. Want a not-clear list")
		Quit method
	End If
	
	If isclear(pfrList2)&not(isclear(pfrList1))
		Do $cinst.$ok(kFalse,pcMessage,"Have a not-clear list. Want a clear list")
		Quit method
	End If
	
	If pfrList1.$linecount<>pfrList2.$linecount
		Do $cinst.$ok(kFalse,pcMessage,con("Have ",pfrList1.$linecount," lines. Want ",pfrList2.$linecount," lines."))
		Quit method
	End If
	
	If pfrList1.$colcount<>pfrList2.$colcount
		Do $cinst.$ok(kFalse,pcMessage,con("Have ",pfrList1.$colcount," columns. Want ",pfrList2.$colcount," columns."))
		Quit method
	End If
End If

If pbTestSmartlist=kTrue
	If pfrList1.$smartlist<>pfrList2.$smartlist
		Do $cinst.$ok(kFalse,pcMessage,con("Have ",pfrList1.$smartlist," smartlist. Want ",pfrList2.$smartlist," smartlist."))
		Quit method
	End If
End If

Calculate lnList1Line as pfrList1.$line
Calculate lnList2Line as pfrList2.$line

Do lrList1Row.$copydefinition(pfrList1)
Do lrList2Row.$copydefinition(pfrList1)

For pfrList1.$line from 1 to pfrList1.$linecount step 1
	Calculate pfrList2.$line as pfrList1.$line
	
	If pfrList1.0.$selected<>pfrList2.0.$selected
		Do $cinst.$ok(kFalse,pcMessage,con("Selections don't match on line ",pfrList1.$line,". Have ",pfrList1.0.$selected,". Want ",pfrList2.0.$selected,"."))
		
		Calculate pfrList1.$line as lnList1Line
		Calculate pfrList2.$line as lnList2Line
		
		Quit method
	End If
	
	If pbTestSmartlist=kTrue
		If pfrList1.$smartlist=kTrue
			If pfrList1.0.$status<>pfrList2.0.$status
				Do $cinst.$ok(kFalse,pcMessage,con("Smartlist statuses don't match on line ",pfrList1.$line,". Have ",pfrList1.0.$status,". Want ",pfrList2.0.$status,"."))
				
				Calculate pfrList1.$line as lnList1Line
				Calculate pfrList2.$line as lnList2Line
				
				Quit method
			End If
		End If
	End If
	
	Do lrList1Row.$assignrow(pfrList1.0,kFalse)
	Calculate lbnList1Row as lrList1Row
	
	Do lrList2Row.$assignrow(pfrList2.0,kFalse)
	Calculate lbnList2Row as lrList2Row
	
	Calculate lbRowsEqual as bincompare(lbnList1Row,lbnList2Row)
	
	If lbRowsEqual=kFalse
		Do pfrList1.$cols.$first() Returns lirColumn
		
		While not(isnull(lirColumn()))
			If isnull(pfrList1.[lirColumn().$name])&not(isnull(pfrList2.[lirColumn().$name]))
				Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have NULL. Want ",pfrList2.[lirColumn().$name],"."))
				Calculate lbQuit as kTrue
			Else If not(isnull(pfrList1.[lirColumn().$name]))&isnull(pfrList2.[lirColumn().$name])
				Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have ",pfrList1.[lirColumn().$name],". Want NULL."))
				Calculate lbQuit as kTrue
			Else If isclear(pfrList1.[lirColumn().$name])&not(isclear(pfrList2.[lirColumn().$name]))
				Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have clear. Want ",pfrList2.[lirColumn().$name],"."))
				Calculate lbQuit as kTrue
			Else If not(isclear(pfrList1.[lirColumn().$name]))&isclear(pfrList2.[lirColumn().$name])
				Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have ",pfrList1.[lirColumn().$name],". Want clear."))
				Calculate lbQuit as kTrue
			Else If lirColumn.$coltype=kItemref
				If pfrList1.[lirColumn().$name]().$fullname<>pfrList2.[lirColumn().$name]().$fullname
					Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have ",pfrList1.[lirColumn().$name]().$fullname,". Want ",pfrList2.[lirColumn().$name]().$fullname,"."))
					Calculate lbQuit as kTrue
				End If
			Else If pfrList1.[lirColumn().$name]<>pfrList2.[lirColumn().$name]
				Do $cinst.$ok(kFalse,pcMessage,con("For column '",lirColumn().$name,"' line ",pfrList1.$line,", have ",pfrList1.[lirColumn().$name],". Want ",pfrList2.[lirColumn().$name],"."))
				Calculate lbQuit as kTrue
			End If
			If lbQuit
				Calculate pfrList1.$line as lnList1Line
				Calculate pfrList2.$line as lnList2Line
				
				Quit method
			End If
			
			Do pfrList1.$cols.$next(lirColumn()) Returns lirColumn
		End While
	End If
End For

Calculate pfrList1.$line as lnList1Line
Calculate pfrList2.$line as lnList2Line

Do $cinst.$ok(kTrue,pcMessage,"Lists agree.")